defmodule Aino.View do
  @moduledoc """
  Compile templates into modules with a render function

  ```elixir
  compile [
    "lib/app/templates/folder/index.html.eex"
  ]
  ```

  Generates the following render functions:

  ```elixir
  def render("index.html", assigns) do
    # compiled index.html.eex
  end
  ```
  """

  defmacro compile(files) when is_list(files) do
    templates =
      Enum.map(files, fn file ->
        Aino.View.compile_template(file)
      end)

    quote do
      def render(filename, assigns \\ %{})

      unquote(templates)
    end
  end

  def compile_template(file) do
    filename = Path.basename(file)
    filename = String.replace(filename, ~r/\.eex$/, "")

    quote bind_quoted: [file: file, filename: filename] do
      require EEx

      compiled = EEx.compile_file(file, [])

      @file file
      @external_resource file
      def render(unquote(filename), var!(assigns)) do
        _ = var!(assigns)
        unquote(compiled)
      end
    end
  end
end
